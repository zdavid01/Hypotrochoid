%picture size settings
expression pictureWidth {200}
expression pictureHeight {200}
dim pictureWidth pictureHeight

 
%main parameters
expression bigR {min(pictureWidth, pictureHeight)/4-10}
expression smallR {bigR *3/5}
expression d {bigR * 1}

%rotation/curve speed parameters
%FI and TH have to be equal
%their value is 2*pi*LCM(R,r)/R
%inner circle rotation speed
point FI 0 0 18.84 18.84
getx fi FI

%drawing line speed
point TH 0 0 18.84 18.84
getx th TH

%outter circle plotting
expression bigCenterx {pictureWidth/2} 
expression bigCentery {pictureHeight/2}
point bigCenter bigCenterx bigCentery

%expression bigR {50}
expression bigOutlinex {bigCenterx + bigR}
point bigOutline bigOutlinex bigCentery


%inner circle plotting
%expression smallR {25}
expression smallOutlinex {bigOutlinex}
expression smallCenterx {bigOutlinex - smallR}
point smallCenter smallCenterx bigCentery
point smallOutline smallOutlinex bigCentery


%D line plotting
%expression d {10}
expression dx {smallCenterx + d}
point D dx bigCentery





%movement of Hypotrochoid curve
expression xphi {
(bigR-smallR)*cos(th) + d*cos((bigR-smallR)/smallR*th) + bigCenterx
}
expression yphi {
(bigR-smallR)*sin(th) - d*sin((bigR-smallR)/smallR*th)
+ bigCentery}

point PT xphi yphi

trace PT 253 144 42



%movement of inner circle
expression bigRt {bigOutlinex - bigCenterx }
expression x1 {cos(fi)*bigRt + bigCenterx}
expression y1 {sin(fi)*bigRt + bigCentery}
point BRT x1 y1

expression smallRt {smallCenterx - bigCenterx}
expression x2 {cos(fi)*smallRt + bigCenterx}
expression y2 {sin(fi)*smallRt + bigCentery}
point SRT x2 y2




%decartes' system
expression angPicturex1 {5}
expression angPicturey1 {5}
expression angPicturex2 {pictureWidth-5}
expression angPicturey2 {pictureHeight-5}

ang_picture angPicturex1 angPicturey1 angPicturex2 angPicturey2 
ang_origin bigCenterx bigCentery

ang_unit 10

ang_drawsystem_p 1 2 2 2 2


%cmark_lt bigCenter
%cmark_lt bigOutline
cmark_rt D
%drawsegment D smallCenter

circle C1 bigCenter bigOutline
drawcircle C1

circle C2 SRT BRT
drawcircle C2

drawsegment SRT PT




animation_frames 300 60


